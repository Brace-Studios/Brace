plugins {
    id 'java'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.jvm' version '1.8.0'
}

group maven_group
version project_version

if (build_number != "undefined") {
    version += "+build.${build_number}"
}

repositories {
    maven {
        url "https://libraries.minecraft.net"
    }
    mavenCentral()
}

dependencies {
    api 'com.mojang:brigadier:1.0.18'
    api 'com.mojang:logging:1.0.0'
    api 'com.mojang:text2speech:1.12.4'

    api 'com.google.code.gson:gson:2.8.9'
}

test {
    useJUnitPlatform()
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task docJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : project.name,
                "Specification-Vendor"    : "Gugle",
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project_version,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

javadoc {
    options.encoding "UTF-8"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            version project.version
            artifactId project.archivesBaseName
            from components.java
            artifact sourcesJar
            artifact docJar
        }
    }
    def ENV = System.getenv()
    if (ENV.MAVEN_URL) {
        repositories.maven {
            url ENV.MAVEN_URL
            credentials {
                username ENV.MAVEN_USERNAME
                password ENV.MAVEN_PASSWORD
            }
        }
    }
}
